; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "LibreOsteo"
#define MyAppVersion "0.6.1"
#define MyAppPublisher "LibreOsteo"
#define MyAppURL "https://www.libreosteo.org"
#define MyAppExeName "LibreOsteo.exe"
#define BuildPath32 "exe.win32-3.8"
#define BuildPath64 "exe.win-amd64-3.8"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{829E22A4-261D-41D8-8987-30A8DD72E895}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
AppCopyright=Copyright (C) 2014-2022 CambiaTech.com
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=C:\Users\jb\libreosteo\LICENSE.md
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: en; MessagesFile: "compiler:Default.isl"
Name: fr; MessagesFile: "compiler:Languages\French.isl"

[Files]
Source: "C:\Users\jb\libreosteo\build\{#BuildPath32}\LibreOsteo.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\jb\libreosteo\build\{#BuildPath32}\manager.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\jb\libreosteo\build\{#BuildPath32}\*.*"; Excludes : "db.sqlite3*,default.log,access.log,errors.log,whoosh_index"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "C:\Users\jb\libreosteo\build\{#BuildPath32}\LibreOsteo.url"; DestDir: "{app}"
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
; Name: "{group}\{#MyAppName} (Service)"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{#MyAppName}"; Filename: "{app}\LibreOsteo.url" ; IconFilename : "{app}\static\images\icon.ico"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\LibreOsteo.url"; \
    IconFilename: "{app}\static\images\icon.ico"; Tasks: desktopicon

[CustomMessages]
; Français
fr.mainusertitle=Utilisateur principal
fr.mainusersubtitle=Définition de l''utilisateur principal
fr.mainuserdescription=Saisissez les informations de l''utilisateur principal de LibreOsteo
fr.login=Login :                                                                          
fr.password=Mot de passe :
fr.inituser=Initialiser l'utilisateur principal  (nouvelle installation uniquement)
fr.installingdb=Installation ou mise à jour de la base de données
fr.installinguser=Création de l'utilisateur principal
fr.installingservice=Installation du service
fr.startingservice=Démarrage du service
fr.uninstallingservice=Désinstallation du service

;English
en.mainusertitle=Main user
en.mainusersubtitle=Description of the main user
en.mainuserdescription=Edit information about the main user of LibreOsteo
en.login=Login :
en.password=Password :
en.inituser=Init the main user (new installation only)
en.installingdb=Installing or updating database
en.installinguser=Creating the main user
en.installingservice=Installing the service
en.startingservice=Starting the service
en.uninstallingservice=Uninstalling the service


[Run]
Filename: "{app}\LibreOsteo.exe"; Parameters: "stop"; StatusMsg: "{cm:uninstallingservice}"; Flags: runhidden
Filename: "{app}\manager.exe"; Parameters: "migrate"; StatusMsg: "{cm:installingdb}"; Flags: runhidden;
Filename: "{app}\manager.exe"; Parameters: "import_zipcodes --json-file zipcode_dataset"; StatusMsg: "{cm:installingdb}"; Flags: runhidden;
Filename: "{app}\manager.exe"; Parameters: "rebuild_index --noinput"; StatusMsg: "{cm:installingdb}"; Flags: runhidden;
Filename: "{app}\LibreOsteo.exe"; Parameters: "--startup auto install"; StatusMsg: "{cm:installingservice}"; Flags: runhidden
Filename: "{app}\LibreOsteo.exe"; Parameters: "start"; StatusMsg: "{cm:startingservice}"; Flags: runhidden

[UninstallRun]
Filename: "{app}\LibreOsteo.exe"; Parameters: "stop"; StatusMsg: "{cm:uninstallingservice}"; Flags: runhidden
Filename: "{app}\LibreOsteo.exe"; Parameters: "remove"; StatusMsg: "{cm:uninstallingservice}"; Flags: runhidden

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; \
    GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Code]

function GetUninstallString(): String;
var
  sUnInstPath: String;
  sUnInstallString: String;
begin
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;


function IsUpgrade(): Boolean;
begin
  Result := (GetUninstallString() <> '');
end;


function UnInstallOldVersion(): Integer;
var
  sUnInstallString: String;
  iResultCode: Integer;
begin
  // 1 - uninstall string is empty
  // 2 - error executing the UnInstallString
  // 3 - successfully executed the UnInstallString

  // default return value
  Result := 0

  // get the uninstall string of the old app
  sUnInstallString := GetUninstallString();
  if sUnInstallString <> '' then begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    if Exec(sUnInstallString, '/VERYSILENT /NORESTART /SUPPRESSMSGBOXES', '', SW_HIDE, ewWaitUntilTerminated, iResultCode) then
      Result := 3
    else 
      Result := 2;
  end else
    Result := 1;
end;


procedure CurStepChanged(CurStep: TSetupStep);
begin
  if (CurStep = ssInstall) then
  begin
    if (IsUpgrade()) then
    begin
      UnInstallOldVersion();
    end;
  end;
end;