; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Libreosteo"
#define MyAppVersion "0.1"
#define MyAppPublisher "Libreosteo"
#define MyAppURL "http://www.facebook.com/Libreosteo"
#define MyAppExeName "Libreosteo.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{829E22A4-261D-41D8-8987-30A8DD72E895}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=C:\project\Libreosteo\LICENCE.md
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: en; MessagesFile: "compiler:Default.isl"
Name: fr; MessagesFile: "compiler:Languages\French.isl"

[Files]
Source: "C:\project\Libreosteo\build\exe.win32-2.7\Libreosteo.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\project\Libreosteo\build\exe.win32-2.7\manager.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\project\Libreosteo\build\exe.win32-2.7\*.*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "C:\project\Libreosteo\build\exe.win32-2.7\Libreosteo.url"; DestDir: "{app}"
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName} (Service)"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{#MyAppName}"; Filename: "{app}\Libreosteo.url" ; IconFilename : "{app}\static\images\icon.ico"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[CustomMessages]
; Français
fr.mainusertitle=Utilisateur principal
fr.mainusersubtitle=Définition de l''utilisateur principal
fr.mainuserdescription=Saisissez les informations de l''utilisateur principal de Libreosteo
fr.login=Login :
fr.password=Mot de passe :
fr.inituser=Initialiser l'utilisateur principal  (nouvelle installation uniquement)
fr.installingdb=Installation ou mise à jour de la base de données
fr.installinguser=Création de l'utilisateur principal

;English
en.mainusertitle=Main user
en.mainusersubtitle=Description of the main user
en.mainuserdescription=Edit information about the main user of Libreosteo
en.login=Login :
en.password=Password :
en.inituser=Init the main user (new installation only)
en.installingdb=Installing or updating database
en.installinguser=Creating the main user

[Tasks]
Name : inituser ; Description: {cm:inituser} ; Flags:checkedonce;

[Run]
Filename: "{app}\manager.exe"; Parameters: "migrate"; StatusMsg: "{cm:installingdb}"; Flags: runhidden;
Filename: "{cmd}" ; Parameters: "/c echo from django.contrib.auth.models import User;User.objects.create_superuser('{code:GetLogin}', '', '{code:GetPassword}') | ""{app}\manager.exe"" ""shell"" "; StatusMsg: "{cm:installinguser}"; Tasks: inituser; Flags : runhidden;

[code]
var
  Page: TInputQueryWizardPage;
  FormPageID : Integer;

procedure InitializeWizard();
begin
  Page := CreateInputQueryPage(wpSelectTasks, ExpandConstant('{cm:mainusertitle}'),ExpandConstant('{cm:mainusersubtitle}'),ExpandConstant('{cm:mainuserdescription}'));

  // Add items (False means it's not a password edit)
  Page.Add(ExpandConstant('{cm:login}'), False);
  Page.Add(ExpandConstant('{cm:password}'), True);

  // Set initial values (optional)
  Page.Values[0] := ExpandConstant('{username}');
  FormPageID := Page.ID;
end;

function GetLogin(Param: String): String;
begin
  Result := Page.Values[0];
end;

function GetPassword(Param: String): String;
begin
  Result := Page.Values[1];
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  // initialize result to not skip any page (not necessary, but safer)
  Result := False;
  // if the page that is asked to be skipped is your custom page, then...
  if PageID = FormPageID then
    // if the component is not selected, skip the page
    Result := not IsTaskSelected('inituser');
end;

